config {
  type: "incremental",
  schema: "tcc_silver",
  description: "Tabela com dados tratados sobre o resumo do setor de transporte aÃ©reo em termos de demanda e oferta.",
  tags: ["silver","anac","oferta_demanda_aerea"],
  bigquery: {
    labels: {
      frequencia: "mensal",
      carga: "incremental"
    },
    partitionBy: "DATE_TRUNC(ano_particao, YEAR)",
    clusterBy: ["aeroporto_destino_sigla"]
  },
}

pre_operations {
  DECLARE ano_particao_checkpoint DEFAULT (
    ${when(incremental(),
    `SELECT max(ano_particao) FROM ${self()}`,
    `SELECT DATE("1999-01-01")`)}
  )
}

SELECT DISTINCT
  empresa_sigla_ AS empresa_sigla,
  empresa_nome_ AS empresa_nome,
  empresa_nacionalidade_ AS empresa_nacionalidade,
  DATE(cast(ano as INT64),cast(mes as INT64), 01) AS data_registro,
  aeroporto_de_origem_sigla_ AS aeroporto_origem_sigla,
  aeroporto_de_origem_nome_ AS aeroporto_origem_nome,
  aeroporto_de_origem_uf_ AS aeroporto_origem_uf,
  aeroporto_de_origem_regiao_ AS aeroporto_origem_regiao,
  aeroporto_de_origem_pais_ AS aeroporto_origem_pais,
  aeroporto_de_origem_continente_ AS aeroporto_origem_continente,
  aeroporto_de_destino_sigla_ AS aeroporto_destino_sigla,
  aeroporto_de_destino_nome_ AS aeroporto_destino_nome,
  aeroporto_de_destino_uf_ AS aeroporto_destino_uf,
  aeroporto_de_destino_regiao_ AS aeroporto_destino_regiao,
  aeroporto_de_destino_pais_ AS aeroporto_destino_pais,
  aeroporto_de_destino_continente_ AS aeroporto_destino_continente,
  natureza AS natureza_voo,
  grupo_de_voo AS tipo_voo,
  CAST(passageiros_pagos AS INT64) AS passageiros_pagos,
  CAST(passageiros_gratis AS INT64) AS passageiros_gratis,
  CAST(carga_paga_kg_ AS INT64) AS carga_paga_kg,
  CAST(carga_gratis_kg_ AS INT64) AS carga_gratis_kg,
  CAST(correio_kg_ AS INT64) AS correio_kg,
  CAST(ask AS INT64) AS ask_assentos_ofercidos_km,
  CAST(rpk AS INT64) AS rpk_passageiros_transportados_km,
  CAST(atk AS INT64) AS atk_toneladas_ofercidas_km,
  CAST(rtk AS INT64) AS rtk_toneladas_transportadas_km,
  CAST(combustivel_litros_ AS INT64) AS combustivel_litros,
  CAST(distancia_voada_km_ AS INT64) AS distancia_voada_km,
  CAST(decolagens AS INT64) AS decolagens,
  CAST(carga_paga_km AS INT64) AS carga_paga_km,
  CAST(carga_gratis_km AS INT64) AS carga_gratis_km,
  CAST(correio_km AS INT64) AS correio_km,
  CAST(assentos AS INT64) AS assentos,
  CAST(payload AS INT64) AS capacidade_total_aeronave_kg,
  CAST(REPLACE(horas_voadas, ",", ".") AS FLOAT64) AS horas_voadas,
  CAST(bagagem_kg_ AS INT64) AS bagagem_kg,
  ano_particao
FROM
  ${ref("tcc_bronze","anac_oferta_demanda_aerea")}
${when(incremental(), 
`WHERE ano_particao = ano_particao_checkpoint
  AND
 DATE(CAST(ano AS INT64), mes, 01) > (SELECT MAX(data_registro) FROM ${self()})`)}