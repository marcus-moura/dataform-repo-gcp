config {
  type: "incremental",
  schema: "tcc_silver",
  description: "Tabela com dados tratados sobre o resumo do setor de transporte aéreo em termos de demanda e oferta.",
  tags: ["silver","anac","oferta_demanda_aerea_basica"],
  uniqueKey: ["id_etapa_basica"],
  bigquery: {
    labels: {
      frequencia: "mensal",
      carga: "incremental"
    },
    partitionBy: "DATE_TRUNC(dt_voo, MONTH)",
    clusterBy: ["nr_voo","id_empresa"]
  },
}

pre_operations {
  DECLARE particao_checkpoint DEFAULT (
    ${when(incremental(),
    `SELECT max(ano_mes_voo) FROM ${self()}`,
    `SELECT DATE("1999-01-01")`)}
  )
}

SELECT DISTINCT
  CAST(id_basica AS INT64)                                                                AS id_etapa_basica,
  CAST(id_empresa AS INT64)                                                               AS id_empresa,
  CAST(nr_voo AS INT64)                                                                   AS nr_voo,
  sg_empresa_icao                                                                         AS sigla_empresa,
  nm_empresa,
  nm_pais                                                                                 AS nm_pais_origem_empresa,
  ds_tipo_empresa                                                                         AS tipo_empresa,
  ds_di                                                                                   AS ds_etapa,
  ds_grupo_di                                                                             AS grupo_voo,
  DATE(dt_referencia)                                                                     AS dt_voo,
  DATE(nr_ano_mes_referencia)                                                             AS ano_mes_voo,
  PARSE_DATETIME('%Y-%m-%d %H:%M:%S', CONCAT(dt_partida_real, ' ', hr_partida_real))      AS dt_partida_real,
  PARSE_DATETIME('%Y-%m-%d %H:%M:%S', CONCAT(dt_chegada_real, ' ', hr_chegada_real))      AS dt_chegada_real,
  CAST(REPLACE(nr_linha, ".0","") AS INT64)                                               AS nr_linha,
  IF(
      UPPER(ds_natureza_tipo_linha) IN ("NÃO IDENTIFICADA", "NÃO IDENTIFICADO"), 
      ds_natureza_etapa, 
      ds_natureza_tipo_linha
    )                                                                                     AS natureza_voo,
  IF( 
      UPPER(ds_tipo_linha) IN ("NÃO IDENTIFICADA", "NÃO IDENTIFICADO"), 
      NULL, 
      ds_tipo_linha
    )                                                                                     AS ds_natureza_voo,
  IF(
      UPPER(ds_servico_tipo_linha) IN ("NÃO IDENTIFICADA", "NÃO IDENTIFICADO"),
      NULL, 
      ds_servico_tipo_linha
    )                                                                                     AS ds_tipo_servico,
  IF(
    UPPER(ds_natureza_etapa) IN ("NÃO IDENTIFICADA", "NÃO IDENTIFICADO"),
    NULL, 
    ds_natureza_etapa
    )                                                                                     AS natureza_etapa,
  sg_icao_origem                                                                          AS sigla_aeroporto_origem,
  nm_aerodromo_origem                                                                     AS nm_aeroporto_origem,
  nm_municipio_origem,
  sg_uf_origem                                                                            AS uf_origem,
  nm_pais_origem,
  sg_icao_destino                                                                         AS sigla_aeroporto_destino,
  nm_aerodromo_destino                                                                    AS nm_aeroporto_destino,
  nm_municipio_destino,
  sg_uf_destino                                                                           AS uf_destino,
  nm_pais_destino,
  CAST(nr_etapa AS INT64)                                                                 AS nr_etapa,
  CAST(nr_escala_destino AS INT64)                                                        AS nr_escala_destino,
  CAST(REPLACE(nr_passag_pagos, ".0","") AS INT64)                                        AS nr_passag_pagos,
  CAST(REPLACE(nr_passag_gratis, ".0","") AS INT64)                                       AS nr_passag_gratis,
  CAST(nr_assentos_ofertados AS INT64)                                                    AS nr_assentos_ofertados,
  CAST(nr_decolagem AS INT64)                                                             AS nr_decolagem,
  CAST(lt_combustivel AS FLOAT64)                                                         AS lt_combustivel,
  CAST(kg_payload AS FLOAT64)                                                             AS kg_payload,
  CAST(kg_bagagem_livre AS FLOAT64)                                                       AS kg_bagagem_livre,
  CAST(kg_bagagem_excesso AS FLOAT64)                                                     AS kg_bagagem_excesso,
  CAST(kg_carga_paga AS FLOAT64)                                                          AS kg_carga_paga,
  CAST(kg_carga_gratis AS FLOAT64)                                                        AS kg_carga_gratis,
  CAST(kg_correio AS FLOAT64)                                                             AS kg_correio,
  CAST(kg_peso AS FLOAT64)                                                                AS kg_peso,
  CAST(km_distancia AS FLOAT64)                                                           AS nr_distancia_km,
  CAST(REPLACE(nr_velocidade_media, ",", ".") AS FLOAT64)                                 AS nr_velocidade_media,
  CAST(REPLACE(nr_horas_voadas, ",", ".") AS FLOAT64)                                     AS nr_horas_voadas,
  CAST(nr_pax_gratis_km AS FLOAT64)                                                       AS nr_pax_gratis_km,
  CAST(nr_carga_paga_km AS FLOAT64)                                                       AS nr_carga_paga_km,
  CAST(nr_carga_gratis_km AS FLOAT64)                                                     AS nr_carga_gratis_km,
  CAST(nr_bagagem_paga_km AS FLOAT64)                                                     AS nr_bagagem_paga_km,
  CAST(nr_bagagem_gratis_km AS FLOAT64)                                                   AS nr_bagagem_gratis_km,
  CAST(nr_correio_km AS FLOAT64)                                                          AS nr_correio_km,
  CAST(nr_ask AS FLOAT64)                                                                 AS nr_ask,
  CAST(nr_rpk AS FLOAT64)                                                                 AS nr_rpk,
  CAST(nr_atk AS FLOAT64)                                                                 AS nr_atk,
  CAST(nr_rtk AS FLOAT64)                                                                 AS nr_rtk,
  data_ingestao,
  nome_arquivo
FROM
  ${ref("tcc_bronze","anac_oferta_demanda_aerea_basica")}
${when(incremental(), 
 `WHERE nr_ano_mes_referencia = particao_checkpoint
  AND DATE(dt_referencia) >= (SELECT MAX(dt_voo) FROM ${self()})`)}


